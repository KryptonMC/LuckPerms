plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.0'
    id 'org.jetbrains.kotlin.kapt' version '1.6.0'
    id 'net.kyori.blossom' version '1.3.0'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    maven { url 'https://repo.kryptonmc.org/releases' }
}

dependencies {
    implementation project(':common')
    compileOnly 'org.kryptonmc:krypton-api:0.56'
    kapt 'org.kryptonmc:krypton-annotation-processor:0.56'

    // The Kotlin plugin adds an implementation one automatically, but Krypton has this bundled
    compileOnly 'org.jetbrains.kotlin:kotlin-stdlib'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs += ['-Xjvm-default=enable']
    }
}

blossom {
    replaceTokenIn('src/main/kotlin/me/lucko/luckperms/krypton/LPKryptonBootstrap.kt')
    replaceToken '@version@', project.ext.fullVersion
}

license {
    include '**/*.kt'
}

shadowJar {
    archiveFileName.set("LuckPerms-Krypton-${project.ext.fullVersion}.jar")

    dependencies {
        include(dependency('net.luckperms:.*'))
        include(dependency('me.lucko.luckperms:.*'))
        include(dependency('com.google.guava:guava:.*'))
        include(dependency('com.google.code.gson:gson:.*'))
    }

    relocate 'net.kyori.event', 'me.lucko.luckperms.lib.eventbus'
    relocate 'com.github.benmanes.caffeine', 'me.lucko.luckperms.lib.caffeine'
    relocate 'okio', 'me.lucko.luckperms.lib.okio'
    relocate 'okhttp3', 'me.lucko.luckperms.lib.okhttp3'
    relocate 'net.bytebuddy', 'me.lucko.luckperms.lib.bytebuddy'
    relocate 'org.mariadb.jdbc', 'me.lucko.luckperms.lib.mariadb'
    relocate 'com.mysql', 'me.lucko.luckperms.lib.mysql'
    relocate 'org.postgresql', 'me.lucko.luckperms.lib.postgresql'
    relocate 'com.zaxxer.hikari', 'me.lucko.luckperms.lib.hikari'
    relocate 'com.mongodb', 'me.lucko.luckperms.lib.mongodb'
    relocate 'org.bson', 'me.lucko.luckperms.lib.bson'
    relocate 'redis.clients.jedis', 'me.lucko.luckperms.lib.jedis'
    relocate 'com.rabbitmq', 'me.lucko.luckperms.lib.rabbitmq'
    relocate 'org.apache.commons.pool2', 'me.lucko.luckperms.lib.commonspool2'
    relocate 'ninja.leaping.configurate', 'me.lucko.luckperms.lib.configurate'
    relocate 'org.yaml.snakeyaml', 'me.lucko.luckperms.lib.yaml'
}

artifacts {
    archives shadowJar
}
